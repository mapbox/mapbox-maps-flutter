group 'com.mapbox.maps.mapbox_maps'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.8.22'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    def token = System.getenv("SDK_REGISTRY_TOKEN") ?: project.findProperty('SDK_REGISTRY_TOKEN')

    if (token == null || token.empty) {
        throw new Exception("SDK Registry token is null. See README.md for more information.")
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url 'https://api.mapbox.com/downloads/v2/releases/maven'
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = "mapbox"
                password = token
            }
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdk 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 21
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

if (file("$rootDir/gradle/ktlint.gradle").exists() && file("$rootDir/gradle/lint.gradle").exists()) {
    project.apply {
        from("$rootDir/gradle/ktlint.gradle")
        from("$rootDir/gradle/lint.gradle")
    }
}

dependencies {
    implementation ("com.mapbox.maps:android:11.2.0-rc.1") {
        exclude group: 'com.mapbox.plugin', module: 'maps-lifecycle'
    }

    implementation "androidx.annotation:annotation:1.7.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.0"
}
